#!/usr/bin/perl
use strict;

# Run one or more test files.

my $usage = "usage: run [-l less.exe] [file.lt | dir]...\n";

use Getopt::Std;

my $testdir   = ".";
my $lesstest  = "$testdir/lesstest";
my $less      = "$testdir/../obj/less";

exit (main() ? 0 : 1);
sub main {
	my %opt;
	die $usage if not getopts('l:', \%opt);
	die $usage if not @ARGV;
	$less = $opt{l} if $opt{l};

	my $errs = 0;
	foreach my $file (@ARGV) {
		$errs += run($file);
	}
	if ($errs > 0) {
		print STDERR "ERRS $errs errors\n";
		return 0;
	}
	return 1;
}

sub run {
	my ($file) = @_;
	if (-d $file) {
		return run_dir($file);
	}
	if ($file !~ /\.lt$/) {
		print "$0: WARNING skipping $file: not .lt file\n";
		return 0;
	}
	if (not -f $file) {
		print STDERR "ERR  cannot open $file\n";
		return 1;
	}
	##print STDERR "FILE $file\n";
	if (system "$lesstest -s $testdir/lt_screen -t $file $less") {
		print STDERR "ERR  running $lesstest\n";
		return 1;
	}
	return 0;
}

sub run_dir {
	my ($dir) = @_;
	my $errs = 0;
	my $dd;
	if (not opendir($dd, $dir)) {
		print STDERR "ERR  cannot open directory $dir\n";
		return 1;
	}
	while (my $entry = readdir($dd)) {
		next if $entry =~ /^\./;
		$errs += run("$dir/$entry");
	}
	closedir $dd;
	return $errs;
}
